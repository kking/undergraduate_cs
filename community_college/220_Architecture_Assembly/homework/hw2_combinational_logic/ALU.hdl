// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.

// File name: ALU.hdl
// Programmer: King
// Date last modified: 12 February, 2019

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // sets to constant 0 if sel==1
    Mux16(a=x, b=false, sel=zx, out=xZero);
    Mux16(a=y, b=false, sel=zy, out=yZero);

    Not16(in=xZero, out=xNot);
    Not16(in=yZero, out=yNot);

    // negates if sel==1
    Mux16(a=xZero, b=xNot, sel=nx, out=xNegate);
    Mux16(a=yZero, b=yNot, sel=ny, out=yNegate);

    Add16(a=xNegate, b=yNegate, out=xySum);
    And16(a=xNegate, b=yNegate, out=xyAnd);
 
    // result==x&y if sel==0, x+y otherwise
    Mux16(a=xyAnd, b=xySum, sel=f, out=result);
  
    Not16(in=result, out=resultNot); 

    // out==result if sel==0, its negation otherwise
    // out[15] is the msb. ng==1 if msb==1, 0 otherwise
    // result bus is split for the following zr circuit
    Mux16(a=result, b=resultNot, sel=no, out=out, out[15]=ng, 
            out[0..7]=mux0To7, out[8..15]=mux8To15);
    
    // zr==1 if Not(Or(result[0..7], result[8..15]))==1
    Or8Way(in=mux0To7, out=or0To7);
    Or8Way(in=mux8To15, out=or8To15);
    Or(a=or0To7, b=or8To15, out=or0To15);
    Not(in=or0To15, out=zr);
}